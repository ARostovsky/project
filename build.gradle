apply plugin: 'groovy'

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/jetbrains/teamcity-rest-client"
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.8'
    compile 'org.apache.ant:ant:1.9.4'
    if (teamcity_rest_client_version == 'sources') {
        compile project(':teamcity-rest-client')
    } else {
        compile "org.jetbrains.teamcity:teamcity-rest-client:$teamcity_rest_client_version"
    }
}

task runScript (dependsOn: 'classes', type: JavaExec) {
    main = 'patchtest'
    classpath = sourceSets.main.runtimeClasspath

    doFirst {
        ext.getProjectProperty = { String propertyName, defaultValue ->
            if (project.hasProperty(propertyName)) {
                String property = project.getProperty(propertyName)
                if (!property.isEmpty()) {
                    return property
                }
            }
            if (defaultValue == null) {
                throw new GradleException("Parameter $propertyName isn't specified")
            }
            return defaultValue
        }

        HashMap<String, Object> argumentsWithDefaultValues = [
                'authUserId'          : null,
                'authPassword'        : null,
                'buildConfigurationID': '',
                'buildId'             : null,
                'customExtensions'    : '',
                'out'                 : 'out',
                'platform'            : null,
                'product'             : null,
                'timeout'             : '60'
        ]

        String argumentsString = argumentsWithDefaultValues
                .collect { key, value -> "'$key': '${getProjectProperty(key, value)}'" }
                .join(", ")

        println(argumentsString)

        args argumentsString
    }
}
